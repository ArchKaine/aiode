plugins {
    id "java"
    id "maven-publish"
    id "application"
    id "org.springframework.boot" version "2.2.1.RELEASE"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "org.liquibase.gradle" version "2.0.1"
}

repositories {
    jcenter()
    mavenCentral()

    maven {
        url = "https://jitpack.io"
    }
}

compileJava.options.encoding = "UTF-8"

dependencies {
    implementation "com.google.apis:google-api-services-youtube:v3-rev212-1.25.0"
    implementation "com.google.guava:guava:28.1-jre"
    implementation "com.sedmelluq:lavaplayer:1.3.29"
    implementation "net.dv8tion:JDA:4.0.0_61"
    implementation "net.robinfriedli:JXP:1.3"
    implementation "net.robinfriedli:StringList:1.5"
    implementation "commons-validator:commons-validator:1.4.0"
    implementation "se.michaelthelin.spotify:spotify-web-api-java:4.1.0"
    implementation "org.apache.commons:commons-collections4:4.1"
    implementation "org.apache.commons:commons-text:1.4"
    implementation "org.glassfish:javax.el:3.0.1-b09"
    implementation "org.postgresql:postgresql:42.2.8"
    implementation "org.quartz-scheduler:quartz:2.3.1"
    implementation "org.hibernate:hibernate-entitymanager:5.4.9.Final"
    implementation "org.ehcache:ehcache:3.8.0"
    implementation "org.hibernate:hibernate-core:5.4.9.Final"
    implementation "org.hibernate:hibernate-c3p0:5.4.9.Final"
    implementation "org.hibernate:hibernate-jcache:5.4.9.Final"
    implementation "org.hibernate:hibernate-validator:5.4.3.Final"
    implementation "org.hibernate:hibernate-validator-cdi:5.4.3.Final"
    implementation("org.liquibase:liquibase-core:3.8.0") {
        exclude group: "ch.qos.logback"
    }
    implementation "org.discordbots:DBL-Java-Library:2.0.1"
    implementation "com.antkorwin:xsync:1.2"

    implementation "org.springframework:spring-core:5.2.1.RELEASE"
    implementation "org.springframework.boot:spring-boot-dependencies:2.2.1.RELEASE"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-jdbc"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"

    liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:3.8'
    liquibaseRuntime sourceSets.main.runtimeClasspath
    liquibaseRuntime sourceSets.main.output
}

group = "botify"
version = "1.0-SNAPSHOT"
description = "botify"
sourceCompatibility = "11"

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

application {
    mainClassName = "net.robinfriedli.botify.boot.SpringBootstrap"
}

def applicationProperties = new Properties()
file("src/main/resources/application.properties").withInputStream { applicationProperties.load(it) }
def privateProperties = new Properties()
file("src/main/resources/settings-private.properties").withInputStream { privateProperties.load(it) }

liquibase {
    activities {
        main {
            changeLogFile applicationProperties.getProperty("liquibase.change-log-path")
            referenceUrl applicationProperties.getProperty("liquibase.referenceUrl")
            url applicationProperties.getProperty("spring.datasource.url")
            username privateProperties.getProperty("spring.datasource.username")
            password privateProperties.getProperty("spring.datasource.password")
            referenceDriver "liquibase.ext.hibernate.database.connection.HibernateDriver"
        }
    }
}